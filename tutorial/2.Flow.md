# Program Flow
So far, all of our programs have been only a few lines that just ran straight through. But as you develop more complex code, you'll eventually want to perform loops, or change the behavior of the program depending on the value stored in a particular register.

While these are core programming concepts, the way they are accomplished on such a constrained system as the Supercon.6 badge might not be as intuitive as you're used to. For example, you won't find the traditional __IF...THEN__ statements among the CPU's opcodes, but there are some equivalent instructions that you'll end up using _a lot_ as you move forward.

# Compare and Skip
The __cp__ (compare) instruction does exactly what it sounds like: compares two values. But there's a bit of a trick, as this is one of those instructions that can only be used with register 0. So any time you want to compare one value to another, you'll always have to stuff it into R0 first.

Using the __cp__ instruction, you'll be able to determine if the given value is the same, less than, or more than what's stored in R0. This is done by checking the status of the C and Z flags after making the comparison.

There's a few things you can do with these flags, but perhaps the most common instruction you'll use in conjunction with them is __skip__, which as you might have guessed, conditionally skips lines in the program based on the status of the flags.

Let's demonstrate with a small program:

```
1001 0000 1111        mov r0, 15      ; Put 15 into R0
0000 0000 0101        cp r0, 5        ; Compare R0 to 5
0000 1111 0001        skip c, 1       ; Skip next line if R0 < 5
1001 0000 0010        mov r0, 2       ; Put 2 into R0
0000 0000 1111        cp r0, 15       ; Compare R0 to 15
0000 1111 1010        skip z, 2       ; Skip next two lines if R0 = 15 
1001 0000 0000        mov r0, 0       ; Put 0 into R0
1001 0001 1111        mov r1, 15      ; Put 15 into R1
```

After running this program, you should see four LEDs on the right side of row 0, indicating that R0 still contains the original value of 15. That's because the two lines which would have changed it were skipped, first because the value in R0 was higher than 5, and again because it was equal to 15.

<img align="right" width="200" src="images/cp_leds.jpg">

Additionally, you should see that there are no LEDs lit on row 1. That's because the second __skip__ instruction actually skipped two lines, not one. The ability to skip multiple instructions is definitely helpful, but keep in mind that you can only jump over a maximum of 4 lines.

Skipping a line after a comparison might seem backwards to modern programmers -- normally the lines following an __IF__ statement are the ones you expect to execute. But as you'll see, skipping instructions can be just as useful as executing them.

# Relative Jumps (Looping)
While there isn't _exactly_ a loop instruction, the 4-bit CPU is able to jump forward and backwards through the program at will, which if carefully utilized, allows you to repeat a given section of your code (or avoid it entirely).

The following combines the __jr__ (jump relative) instruction with __inc__, __cp__, and __skip__ to demonstrate how the flow of a program can be controlled:

```
1001 0000 0001        mov r0, 1       ; Put 1 into R0
0000 0010 0000        inc r0          ; Increment R0
0000 0000 0101        cp r0, 5        ; Compare R0 to 5
0000 1111 1001        skip z, 1       ; Skip next line if R0 = 5
1111 1111 1100        jr -4           ; Jump back 4 lines
1001 0001 1111        mov r1, 15      ; Put 15 into R1
```

There's a bit going on here, so let's walk through it step-by-step.

<img align="right" width="200" src="images/leds_loop.jpg">

First we place a 1 in R0, and then increment it. At this point, R0 equals 2. We compare it to 5, find that it's not equal, so we _do not_ skip the __jr -4__ instruction. This makes the program go back 4 lines (the __jr__ instruction itself counts as one line) to __inc r0__. We now have a loop that will continue until R0 equals 5.

Once that happens, the loop exits, and our final instruction can execute. The resulting LEDs should look like the image on the right -- with 5 in R0 and 15 in R1.

# Decrement and Skip
As mentioned previously, the __inc__ and __dec__ instructions are very handy, especially when combined with __jr__ and __skip__ so much so that there's actually a ready-made combo instruction you can use that makes things a little easier: __dsz__

This instruction will decrement a given register until it equals zero, and when it does, skips the next instruction. Here's a brief example:

```
1001 0000 0101        mov r0, 5       ; Put 5 into R0
1001 0001 1010        mov r1, 10      ; Put 10 into R1
0000 0010 0001        inc r1          ; Increment R1
0000 0100 0000        dsz r0          ; Decrement R0 until 0
1111 1111 1101        jr -3           ; Jump back 3 lines
1001 0010 1111        mov r2, 15      ; Put 15 into R2
```

<img align="right" width="200" src="images/leds_dsz.jpg">

In this example, we increment R1 while we decrement R0, with a __jr__ instruction to continue looping around until __dsz__ sees that R0 equals 0 and skips it. The end result, as shown on the right, should be no LEDs lit on row 0, and four each in rows 1 and 2.

As you can see, this instruction is perfect for when you want to repeat an action a specific number of times. It saves you a couple lines of code compared to doing it manually, plus you don't have to remember which flags mean what since it's hard-coded to look for the equal condition.

# Division (Revisited)
Now that we've covered loops and conditional control of the program flow, let's tackle the division program mentioned in the previous chapter:

```
1001 0000 1111        mov r0, 15      ; Put 15 into R0
1001 0001 0011        mov r1, 3       ; Put 3 into R1
1001 0010 0001        mov r2, 1       ; Start result counter at 1

0011 0000 0001        sub r0, r1      ; Subtract R1 from R0
0000 1111 1010        skip z, 2       ; Skip next two lines if result is zero
0000 0010 0010        inc r2          ; Increment counter
1111 1111 1100        jr -4           ; Jump back to division
```

First the dividend (15) goes into R0, and the divisor (3) is held in R1. The program then uses a loop to keep subtracting R1 from R0 until the result is 0. We don't need to use __cp__ here, as the __sub__ instruction will handily raise the Z flag for us. Once R0 is empty, the next two lines are skipped so that the loop can exit.

<img align="right" width="200" src="images/leds_division.jpg">

The final result should look like the image on the right -- row 0 will show no LEDs, row 1 will have 0011 (3), and row 2 will display the answer to our division problem: 5 (0101).

But wait a minute...what happens if there's a remainder? Or you want to divide a small number by a larger one? Well, that's a good question, you should look into it and find out. After all, we can't show you everything here.

That wraps up the _Program Flow_ chapter, click the arrow button below to _Hardware I/O_.

***
<a href="3.IO.md"><img align="right" width="100" src="images/next.png"></a>
